# Initializations

esp := 0x32ff5c

load sections .text, .rdata, .data from file

lastError<32> := nondet

inputState<32> := 0
outputState<8> := 0
isUppercaseAlphabet<1> := true
isLowercaseAlphabet<1> := true

# Import table

@[0x402000, 4] := 0x7b442e00 as ReadFile
@[0x402004, 4] := 0x7b43111c as FindFirstFileA
@[0x402008, 4] := 0x7b432aa8 as SetConsoleTextAttribute
@[0x40200c, 4] := 0x7b431a7c as GetStdHandle
@[0x402010, 4] := 0x7b43b540 as WriteConsoleA
@[0x402014, 4] := 0x7b442ef0 as WriteFile
@[0x402018, 4] := 0x7b431180 as FindNextFileA
@[0x40201c, 4] := 0x7b43ac40 as ReadConsoleA
@[0x402020, 4] := 0x7b432ae4 as SetCurrentDirectoryA
@[0x402024, 4] := 0x7b43175c as GetLastError
@[0x402028, 4] := 0x7b430a64 as CreateFileA
@[0x40202c, 4] := 0x7b430884 as CloseHandle
@[0x402030, 4] := 0x7b454890 as ExitProcess

# Dynamic function mocks

## ReadFile

replace ReadFile(hFile, lpBuffer, nNumberOfBytesToRead, lpNumberOfBytesRead, lpOverlapped) by
  nNumberOfBytesRead<32> := 0
  # assume ReadFile is only used to read in "latin_alphabet.txt.encrypted"
  assert hFile = 0x34
  assert lpOverlapped = 0
  if inputState + nNumberOfBytesToRead < 26 then
    nNumberOfBytesRead := nNumberOfBytesToRead
  else
    nNumberOfBytesRead := 26 - inputState
  end
  for i<32> in 0 to nNumberOfBytesRead - 1 do
    @[lpBuffer + i] := case inputState is
      00: 0x0f
      01: 0xce
      02: 0x60
      03: 0xbc
      04: 0xe6
      05: 0x2f
      06: 0x46
      07: 0xea
      08: 0x07
      09: 0xca
      10: 0x62
      11: 0xbd
      12: 0x67
      13: 0xef
      14: 0x26
      15: 0xda
      16: 0x1f
      17: 0xc6
      18: 0x64
      19: 0xbe
      20: 0xe7
      21: 0xaf
      22: 0x06
      23: 0xca
      24: 0x17
      25: 0xc2
       _: @[lpBuffer + i]
    inputState := inputState + 1
  end
  # write the result in the pointer passed in argument, if any
  if lpNumberOfBytesRead <> 0 then
    @[lpNumberOfBytesRead, 4] := nNumberOfBytesRead
  end
  # return a non zero value
  return 1
end

## FindFirstFileA

replace FindFirstFileA(lpFileName, lpFindFileData) by
  assert @[lpFileName, 12] = "*.encrypted"z
  assert lpFindFileData <> 0
  @[lpFindFileData + 44, 29] := "latin_alphabet.txt.encrypted"z
  return 0x110a58
end

## SetConsoleTextAttribute

# we ignore hConsoleOutput
# we ignore wAttributes
replace SetConsoleTextAttribute(_,_) by
  return 1
end

## GetStdHandle

replace GetStdHandle(nStdHandle) by
  case nStdHandle is
    -10: eax := 23
    -11: eax := 27
    _: assert false
  end
  return
end

## WriteConsoleA

# we ignore lpBuffer
replace WriteConsoleA(hConsoleOutput, _, nNumberOfCharsToWrite, lpNumberOfCharsWritten, lpReserved) by
  assert hConsoleOutput = 27
  assert lpReserved = 0
  if lpNumberOfCharsWritten <> 0 then
    @[lpNumberOfCharsWritten, 4] := nNumberOfCharsToWrite
  end
  return 1
end

## WriteFile

replace WriteFile(hFile, lpBuffer, nNumberOfBytesToWrite, lpNumberOfBytesWritten, lpOverlapped) by
  # assume WriteFile is only used to read in "latin_alphabet.txt"
  assert hFile = 0x38
  assert lpOverlapped = 0
  for i<32> in 0 to nNumberOfBytesToWrite - 1 do
    isUppercaseAlphabet := isUppercaseAlphabet && @[lpBuffer + i] = "A" + outputState
    isLowercaseAlphabet := isLowercaseAlphabet && @[lpBuffer + i] = "a" + outputState
    outputState := outputState + 1
  end
  if lpNumberOfBytesWritten <> 0 then
    @[lpNumberOfBytesWritten, 4] := nNumberOfBytesToWrite
  end
  return 1
end

## FindNextFileA

# we ignore lpFindFileData
replace FindNextFileA(hFindFile,_) by
  assert hFindFile = 0x110a58
  # force no other file
  lastError := 18
  return 0
end

## ReadConsoleA

replace ReadConsoleA(hConsoleInput, lpBuffer, nNumberOfCharsToRead, lpNumberOfCharsRead, pInputControl) by
  # assume ReadConsoleA is only used to read in STD_INPUT_HANDLE
  assert hConsoleInput = 23
  assert pInputControl = 0
  # create a symbolic value and add a constraint to the number of read bytes
  nNumberOfBytesRead<32> := nondet
  assume 0 <= nNumberOfBytesRead <= nNumberOfCharsToRead
  for i<32> in 0 to nNumberOfBytesRead - 1 do
    # create a new symbolic byte in the byte stream bRead
    @[lpBuffer + i] := nondet as bRead
    # such that it is printable
    assume " " <= bRead <= "~"
  end
  # mimic the 'enter' keystroke
  if nNumberOfBytesRead < nNumberOfCharsToRead - 1 then
    @[lpBuffer + i] := "\r"
    nNumberOfBytesRead := nNumberOfBytesRead + 1
    if nNumberOfBytesRead < nNumberOfCharsToRead - 1 then
      @[lpBuffer + i + 1] := "\n"
      nNumberOfBytesRead := nNumberOfBytesRead + 1
    end
  end
  # write the result in the pointer passed in argument, if any
  if lpNumberOfCharsRead <> 0 then
    @[lpNumberOfCharsRead, 4] := nNumberOfBytesRead
  end
  # return a non zero value
  return 1
end

## SetCurrentDirectoryA

replace SetCurrentDirectoryA(lpPathName) by
  # assume that the directory is "Files"
  assert @[lpPathName, 6] = "Files"z
  return 1
end

## GetLastError

replace GetLastError by
  return lastError
end

## CreateFileA

# we ignore dwDesiredAccess, dwShareMode, lpSecurityAttributes, dwCreationDisposition, dwFlagsAndAttributes
replace CreateFileA(lpFileName, _, _, _, _, _, hTemplateFile) by
  assert hTemplateFile = 0
  if @[lpFileName, 29] = "latin_alphabet.txt.encrypted"z then
    eax := 0x34
  else
    assert @[lpFileName, 19] = "latin_alphabet.txt"z
    eax := 0x38
  end
  return
end

## CloseHandle

replace CloseHandle(_) by
  return 1
end

## ExitProcess

halt at ExitProcess

# Goals

reach ExitProcess such that isUppercaseAlphabet || isLowercaseAlphabet
then print ascii stream bRead
